plugins {
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id 'org.spongepowered.mixin' version '0.7.+'
    id "com.modrinth.minotaur" version "2.8.10"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

version = "${project_version}-1.16.5-Forge"

minecraft {
    mappings channel: 'official', version: '1.16.5'
    copyIdeResources = true
}

dependencies {
    minecraft "net.minecraftforge:forge:1.16.5-${forge_16}"

    compileOnly fg.deobf("pixelmon:Pixelmon-1.16.5-${pixelmon_16}-server:${pixelmon_16}")
    compileOnly fg.deobf("maven.modrinth:extralib:${extralib}-forge,1.16.5")

    annotationProcessor 'org.spongepowered:mixin:0.8.3:processor'
}

mixin {
    add sourceSets.main, 'mixins.legendcontrol.refmap.json'
    config 'mixins.legendcontrol.json'
}

processResources {
    filesMatching('META-INF/mods.toml') {
        expand(
                'project_name': project_name,
                'project_version': project_version
        )
    }
}

def gradleProperties = new Properties()
def gradlePropertiesFile = new File(rootProject.file("/.gradle/gradle.properties").path)

if (gradlePropertiesFile.exists()) {
    gradlePropertiesFile.withInputStream { gradleProperties.load(it) }
}

modrinth {
    token = gradleProperties.getProperty("MODRINTH_API_KEY")
    projectId = "legendcontrol"
    versionName = "${project_name}-${version}"
    versionNumber = project_version
    versionType = "release"
    changelog = rootProject.file("changelog.md").text
    uploadFile = jar
    gameVersions = ["1.16.5"]
    loaders = ["forge"]
    dependencies {
        required.project "pixelmon"
        required.project "extralib"
    }
}

jar.finalizedBy('reobfJar')
build.finalizedBy('versionedRelease')