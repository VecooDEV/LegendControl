subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    group = 'com.vecoo.legendcontrol'
    version = '2.4.1'

    ext {
        forgeVersion = "none"
    }

    jar {
        setArchiveBaseName("${rootProject.name}")
    }

    repositories {
        mavenCentral()
        maven { url = "https://repo.spongepowered.org/maven" }
        maven { url = "https://api.modrinth.com/maven" }
        ivy {
            setUrl('https://download.nodecdn.net/containers/reforged/server/release')
            metadataSources {
                artifact()
            }
            patternLayout {
                artifact('[revision]/[artifact].[ext]')
            }
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    task versionedRelease(type: Copy) {
        dependsOn('shadowJar')
        delete fileTree('../release/') {
            include '**/*.jar'
        }
        group "build"
        from("./build/libs/${rootProject.name}-${version}.jar")
        into('../release/')
        include '*.jar'
        rename { String filename ->
            filename.replace("-${version}.jar", "-${project.version}-${forgeVersion}.jar")
        }
    }
}