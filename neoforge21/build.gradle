plugins {
    id 'net.neoforged.gradle.userdev' version '7.0.184'
    id "com.modrinth.minotaur" version "2.8.7"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

version = "${project_version}-1.21.1-NeoForge"

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {}

dependencies {
    implementation "net.neoforged:neoforge:${neoforge_version_21}"

    compileOnly "pixelmon:Pixelmon-1.21.1-${pixelmon_version_21}-server:${pixelmon_version_21}"
    compileOnly "maven.modrinth:extralib:${extralib_version}-neoforge,1.21.1"
}

processResources {
    filesMatching('META-INF/neoforge.mods.toml') {
        expand(
                'project_name': project_name,
                'project_version': project_version
        )
    }
}

def gradleProperties = new Properties()
def gradlePropertiesFile = new File(rootProject.file("/.gradle/gradle.properties").path)

if (gradlePropertiesFile.exists()) {
    gradlePropertiesFile.withInputStream { gradleProperties.load(it) }
}

modrinth {
    token = gradleProperties.getProperty("modrinth_api_key")
    projectId = "legendcontrol"
    versionName = "${project_name}-${version}"
    versionNumber = project_version
    versionType = "release"
    changelog = rootProject.file("changelog.md").text
    uploadFile = jar
    gameVersions = ["1.21.1"]
    loaders = ["neoforge"]
    dependencies {
        required.project "extralib"
        required.project "pixelmon"
    }
}

build.finalizedBy('versionedRelease')